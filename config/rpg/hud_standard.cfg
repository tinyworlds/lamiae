hudiconname = [
	local ret
	ret = "default"
	loopwhile i $numargs [ strcmp $ret "default" ] [
		ind = (+ $i 1)
		do [
			if (strlen $arg@ind) [
				ret = $arg@ind
			]
		]
	]

	result $ret
]

hudequip = [
	//weapon, ammo, x, y
	local SIZE OFFSET
	SIZE = 160
	OFFSET = (+ $SIZE 16)

	col = (? (> (r_get_lastaction player) $lastmillis) 0x7F7F7F 0xFFFFFF)
	if (= (r_reftype $arg1) $REF_EQUIP) [
		r_select_item $arg1 [ r_select_item_use $arg1 [
			local ammo

			if (&& (>= $r_item_use_type $USE_WEAPON) [
				ammo = (indexof $resv_ammo $r_item_use_ammo)
				|| $r_item_use_cost (< $ammo 0)
			]) [
				r_hud_image (r_iconpath "empty") $arg3 $arg4 $SIZE $SIZE $col
				r_hud_image (r_iconpath (hudiconname $r_item_use_icon $r_item_icon)) $arg3 $arg4 $SIZE $SIZE $col

				amount = 0
				r_hud_image (r_iconpath "empty") (+f $arg3 $OFFSET) $arg4 $SIZE $SIZE $col

				if (>= $ammo 0) [
					r_select_char player [ amount = $r_char_@(at $resv_ammo_calls $ammo) ]
					if (< $amount $r_item_use_cost) [ col = 0xFF7F7F ]
					r_hud_image (r_iconpath (at $resv_ammo $ammo)) (+f $arg3 $OFFSET) $arg4 $SIZE $SIZE $col
				] [
					valid = 0
					r_select_static_ammo $r_item_use_ammo [
						loopwhile i (r_ammo_num_item) [ = $valid 0 ] [
							if (= (r_get_base $arg1) (r_ammo_get_item $i)) [
								valid = 2
								amount = (r_get_amount player (r_get_base $arg1))
							]
							if (= (r_reftype $arg2) $REF_EQUIP) [
								if (= (r_get_base $arg2) (r_ammo_get_item $i)) [
									valid = 1
									amount = (r_get_amount player (r_get_base $arg2))
								]
							]
						]
					]

					if (< $amount $r_item_use_cost) [ col = 0xFF7F7F ]

					case $valid 0 [
						r_hud_image (r_iconpath "invalid") (+f $arg3 $OFFSET) $arg4 $SIZE $SIZE $col
					] 1 [
						r_select_item $arg2 [
							r_select_item_use $arg2 [
								r_hud_image (r_iconpath (hudiconname $r_item_use_icon $r_item_icon)) (+f $arg3 $OFFSET) $arg4 $SIZE $SIZE $col
							]
						]
					] 2 [
						r_hud_image (r_iconpath (hudiconname $r_item_use_icon $r_item_icon)) (+f $arg3 $OFFSET) $arg4 $SIZE $SIZE $col
					]
				]

				tw = (divf (text_width (* $amount 1)) 2)
				r_hud_text (- $arg3 -256 $tw) (+ $arg4 16) 1 $col (* $amount 1)

				tw = (divf (text_width (concatword "-" $r_item_use_cost)) 2)
				r_hud_text (- $arg3 -256 $tw) (+ $arg4 84) 1 $col (concatword "-" $r_item_use_cost)

			] [
				r_hud_image (r_iconpath "empty") (+f $arg3 112) $arg4 $SIZE $SIZE $col
				r_hud_image (r_iconpath (hudiconname $r_item_use_icon $r_item_icon)) (+f $arg3 112) $arg4 $SIZE $SIZE $col
			]
		]]
	] [
		r_hud_image (r_iconpath "empty") (+ $arg3 96) $arg4 $SIZE $SIZE $col
	]
]

hudcharge = [ //attacking weapon, owner, x, y, dx, dy
	progress = 0
	threshold = 0
	if (= (r_reftype $arg1) $REF_EQUIP) [
		r_select_item_use $arg1 [
			threshold = (divf $r_item_use_mincharge $r_item_use_maxcharge)
			progress = (divf (r_get_charge $arg2) (*f $r_item_use_charge $r_item_use_maxcharge))
		]
	]

	r_hud_image (r_iconpath "hback") $arg3 $arg4 $arg5 $arg6 0x0
	r_hud_horizbar (r_iconpath "hbar") $arg3 $arg4 $arg5 $arg6 $progress (? (>f $progress $threshold) 0x00FF00 0xFF0000)

	r_hud_solid (+f $arg3 (*f $threshold $arg5) -3) $arg4 6 $arg6 0xFFFF00
]

draweffects = [ //efx string, y
	x = (-f (divf $hud_right 2) (*f 36 (listlen $arg1)))
	looplist img $arg1 [
		r_hud_image (r_iconpath $img) $x $arg2 64 64 0xFFFFFF
		x = (+f $x 72)
	]
]

statusbar = [ //x, y, dx, dy, cur, max, colour, pulse, left (text)
	local x y dx dy col backcol
	dx = $arg3
	dy = $arg4
	col = $arg7

	if (&& $arg8 [<f (divf $arg5 $arg6) .25]) [ //pulse
		x = (+f $arg1 (*f 8 (sin $lastmillis)))
		y = (+f $arg2 (*f 8 (cos $lastmillis)))
		backcol = 0x0
		if (& $col 0xFF) [
			backcol = (+ 127 (*f 127.5 (sin $lastmillis)))
		]
		if (& $col 0xFF00) [
			backcol = (| $backcol (<< (+ 127 (*f 127.5 (sin $lastmillis))) 8))
		]
		if (& $col 0xFF0000) [
			backcol = (| $backcol (<< (+ 127 (*f 127.5 (sin $lastmillis))) 16))
		]
	] [
		x = $arg1
		y = $arg2
		backcol = (| (>> (& $col 0xFF) 2) (<< (>> (& $col 0xFF00) 10) 8) (<< (>> (& $col 0xFF0000) 18) 16))
	]

	r_hud_image (r_iconpath "hback") $x $y $dx $dy $backcol
	r_hud_horizbar (r_iconpath "hbar") $x $y $dx $dy (divf $arg5 $arg6) $col

	if $arg9 [
		r_hud_text (+f $x (*f $dx 0.125)) $y (divf $dy 64) (& (~ $backcol) 0xFFFFFF) (* 1 $arg5)
	] [
		local text offset
		text = (* 1 $arg5)
		offset = (*f (text_width $text) (divf $dy 64))
		r_hud_text (-f (+f $x (*f $dx 0.125)) $offset) $y (divf $dy 64) (& (~ $backcol) 0xFFFFFF) (* 1 $arg5)
	]
]

drawhotkey = [ //x, y, dx, dy, idx
	r_hud_image (r_iconpath "empty") $arg1 $arg2 $arg3 $arg4 0xFFFFFF
// 	idx = (at (hotkey_get $arg5) 0)
// 	use = (at (hotkey_get $arg5) 1)
//
// 	if (> $idx 0) [
// 		image = "default"
// 		r_select_item $idx [
// // 			r_select_item_use $idx [
// // 				if $r_item_use_icon
// //
// // 			]
// 			if $r_item_icon [
// 				image = $r_item_icon
// 			]
// 		]
//
// 		r_hud_image (r_iconpath $image) $arg1 $arg2 $arg3 $arg4 (? (> (r_get_amount player $idx) 0) 0xFFFFFF 0x3F3F3F)
// 	]
//
	r_hud_text (-f $arg1 8) $arg2 .5 0xFFFFFF (+ $arg5 1)
]

r_hud "main" [
]
